import "res://resources/dialogue/talk_ouroboros.dialogue" as ouroboros
import "res://resources/dialogue/check_ground.dialogue" as physics_layer

~ revealer
({{revealer}}.)
- Resume => end_browsing
- Info => revealer_info
- Instruction => revealer_instruction
- Check surface [if is_checking_surface] => revealer_surface

~ revealer_info
{{green}}: (A pair of glasses that I received from {{yellow}}. It has a golden frame, but it's fake gold. One of the lens holders has an improvised lens system attached. The other is covered.)
=> revealer

~ revealer_instruction
{{green}}: (If I am near a thing or person, close enough to interact with them, and then bring this item out again, I might be able to make something happen.)
=> revealer

~ revealer_surface
do reveal_surface()
=> end_browsing

~ ouroboros
(The {{ouroboros}} is floating above my head.)
- Resume => end_browsing
- Talk => ouroboros_then_end_browsing
=> end_browsing

~ ouroboros_real_space
(The {{ouroboros}} reappeared, floating above my head.)
- Resume => ouroboros/ouroboros_real_space_end_browsing
- Talk => ouroboros_real_space_then_end_browsing
=> END!

~ ouroboros_then_end_browsing
=>< ouroboros/ouroboros_info
=> end_browsing

~ ouroboros_real_space_then_end_browsing
=>< ouroboros/ouroboros_real_space_info
=> end_browsing

~ ice_skates
({{skates}}.)
- Resume => end_browsing
- Equip => ice_skates_equip
- Info => ice_skates_info
- Instruction => ice_skates_instruction

~ ice_skates_equip
do set_player_state("player_skating_state")
do set_player_anim("down")
do set_item_browse_dialogue_node("ice_skates", "ice_skates_equipped")
do set_item_items_state("ice_skates", "items_below_state")
=> end_browsing

~ ice_skates_equipped
({{skates}}.)
- Resume => end_browsing
- Unequip => ice_skates_unequip
=> end_browsing

~ ice_skates_unequip
if is_player_on_water
	=>< physics_layer/water
else
	do set_player_state("player_ordinary_state")
	do set_player_anim("down")
	do set_item_browse_dialogue_node("ice_skates", "ice_skates")
	do set_item_items_state("ice_skates", "items_exhibit_state")
=> end_browsing

~ ice_skates_info
{{green}}: (An ice skating set. I made these from shards of ice in the {{imaginary}}. But somehow, these skates feel real.)
=> end_browsing

~ ice_skates_instruction
{{green}}: (These skates produce ice when I move. Thanks to that, should be able to cross water.)
=> end_browsing

~ end_browsing
do set_player_anim("default")
=> END!